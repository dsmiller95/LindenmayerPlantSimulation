#define resourceNodeTargetMax 4

## S is a resource source node
#symbols S
#matches S
#export SourceNode S
S(x) -> S(x)B(x)

## R is a resource diffusion/distribution node
#symbols R
#matches R
#export DistributorNode R

       R(x) > A(a) -> R(x + a)
B(b) < R(x) > A(a) -> R(x + a + b)
B(b) < R(x)        -> R(x + b)
       R(x)        -> R(x)

## Randomly decide whether or not to pull resources forward
## P(0.2) | R(a)Q < R(x) : a >= 1 && x < resourceNodeTargetMax -> A(-a / 2)R(x + a / 2)
## P(0.8) | R(a)Q < R(x) : a >= 1 && x < resourceNodeTargetMax -> R(x)


## W is a node which pull resources forward from the base into a branch
##	Pulls resources when any are available from the root node, and will generate
##	a back-propigating negative command and a forward-propigating positive command
## Randomly decide whether or not to pull resources forward

#symbols W
#matches W
#export LeafNode W
P(0.5) | R(a)Q < W > R(x) : a >= 1 && x < a -> A(-(a - x) * .9)WB((a - x) * .9)
P(0.5) | R(a)Q < W > R(x) : a >= 1 && x < a -> W



## A is a resource amount, propigating downwards, to be captured by a resource node
#symbols A
#matches A
#export DownwardSubtraction A
A(x)        -> 
A(x) > A(y) -> A(y)

## Q is a resource amount downpropigation node
#symbols Q
#matches Q
#export LeafBaseNode Q

Q > [A(x)][A(y)][A(z)][A(c)] -> A(x + y + z + c)Q
Q > [A(x)][A(y)][A(z)] -> A(x + y + z)Q
Q > [A(x)][A(y)] -> A(x + y)Q
Q > [A(x)] -> A(x)Q

## B is a resource amount, propigating upwards, to be captured by a resource node
#symbols B
#matches B
#export UpwardAddition B

       B(x) -> 
B(y) < B(x) -> B(y)

